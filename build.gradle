import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompilationTask

plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.9.0'
	id 'application'
}

group = 'org.koitharu'
version = '0.5'

repositories {
	mavenCentral()
	google()
	maven {
		url 'https://jitpack.io'
	}
}

test {
	useJUnitPlatform()
}

tasks.named('compileKotlin', KotlinCompilationTask.class) {
	compilerOptions.jvmTarget.set(JvmTarget.JVM_11)
	compilerOptions.freeCompilerArgs.add('-opt-in=kotlinx.coroutines.ExperimentalCoroutinesApi')
	compilerOptions.freeCompilerArgs.add('-opt-in=kotlin.ExperimentalStdlibApi')
}

compileTestKotlin {
	kotlinOptions.jvmTarget = '11'
}

application {
	mainClassName = 'org.koitharu.kotatsu_dl.MainKt'
}

jar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	manifest {
		attributes 'Main-Class': 'org.koitharu.kotatsu_dl.MainKt'
	}
	from {
		configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
	}
}


dependencies {
	implementation 'com.github.nv95:kotatsu-parsers:8e452f4271'

	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'
	implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-swing:1.7.3'

	implementation 'com.squareup.okhttp3:okhttp:4.11.0'
	implementation 'com.squareup.okio:okio:3.4.0'
	implementation 'org.json:json:20230618'
	implementation 'io.webfolder:quickjs:1.1.0'

	testImplementation 'org.jetbrains.kotlin:kotlin-test'
}